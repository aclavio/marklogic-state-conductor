plugins {
	id "java"
	id "maven-publish"
	id "com.jfrog.bintray" version "1.8.4"
	id "net.saliman.properties" version "1.5.1"
}

repositories {
	jcenter()
	mavenLocal()
}

group = "com.marklogic"
version = "0.4.0"

configurations {
  modules
}

task modulesJar(type: Jar) {
  description = "Create a jar of the source-conductor modules and configurations into a package for publishing"
  from("src/main/ml-config") {
    into("marklogic-state-conductor/ml-config")
  }
  from("src/main/ml-modules") {
    into("marklogic-state-conductor/ml-modules")
  }
  destinationDir file("build/libs")
  baseName "marklogic-state-conductor-cpf"
}

artifacts {
  modules modulesJar
}

publishing {
	publications {
		mainModules(MavenPublication) {
			artifactId "marklogic-state-conductor-cpf"
			artifact modulesJar
		}
	}
}

if (project.hasProperty("myBintrayUser")) {
	bintray {
		user = project.findProperty("myBintrayUser")
		key = project.findProperty("myBintrayKey")
		publications = ["mainModules"]
		pkg {
			repo = "maven"
			name = "marklogic-state-conductor-cpf"
			licenses = ["Apache-2.0"]
			websiteUrl = "https://github.com/aclavio/marklogic-state-conductor"
			vcsUrl = "https://github.com/aclavio/marklogic-state-conductor.git"
			issueTrackerUrl = "https://github.com/aclavio/marklogic-state-conductor/issues"
			githubRepo = "aclavio/marklogic-state-conductor"
			githubReleaseNotesFile = "README.md"
			labels = ['marklogic']
			version {
				name = project.version
				released = new Date()
			}
		}
	}
}
