plugins {
  id "java"
  id "maven-publish"
  id 'signing'
  id "net.saliman.properties" version "1.5.1"
}

configurations {
  modules
}

task modulesJar(type: Jar) {
  description = "Create a jar of the source-conductor modules and configurations into a package for publishing"
  from("src/main/ml-config") {
    into("marklogic-state-conductor/ml-config")
  }
  from("src/main/ml-modules") {
    into("marklogic-state-conductor/ml-modules")
  }
  from("src/main/ml-data") {
    into("marklogic-state-conductor/ml-data")
  }
  destinationDir file("build/libs")
  baseName "marklogic-state-conductor"
}

artifacts {
  modules modulesJar
}

publishing {
  publications {
    mainModules(MavenPublication) {
      artifactId "marklogic-state-conductor"
      artifact modulesJar
      pom {
        name = 'MarkLogic State Conductor'
        description = 'An event-based state-machine engine for manipulating MarkLogic database documents.'
        url = 'https://github.com/aclavio/marklogic-state-conductor'
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            name = 'Anthony Clavio'
            email = 'anthony.clavio@marklogic.com'
            organization = 'MarkLogic'
            organizationUrl = 'https://www.marklogic.com'
          }
          developer {
            name = 'MarkLogic Github Contributors'
            email = 'general@developer.marklogic.com'
            organization = 'Github Contributors'
            organizationUrl = 'https://github.com/aclavio/marklogic-state-conductor/graphs/contributors'
          }
        }
        scm {
          url = 'git@github.com:aclavio/marklogic-state-conductor.git'
          connection = 'scm:git@github.com:aclavio/marklogic-state-conductor.git'
          developerConnection = 'scm:git@github.com:aclavio/marklogic-state-conductor.git'
        }
        issueManagement {
          system = 'GitHub Issues'
          url = 'https://github.com/aclavio/marklogic-state-conductor/issues'
        }
      }
    }
  }
  repositories {
    maven {
      credentials {
        username = project.hasProperty('mavenUser') ? project.property('mavenUser') : System.getenv('MAVEN_USER')
        password = project.hasProperty('mavenPassword') ? project.property('mavenPassword') : System.getenv('MAVEN_PASSWORD')
      }
      url "build/libs"
    }
  }
}

signing {
  useGpgCmd()
  sign publishing.publications.mainModules
}
